// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Various combinations of input switches for determining
    // whether to load a new value or zero into the register,
    // or load the next incremented value into the register.
    Or(a=load, b=reset, out=loadOrReset);
    Or(a=loadOrReset, b=inc, out=loadReg);

    // Determines which input to load into the register next.
    // Selects between the load value, zero, or the loopback/incremented value.
    Mux16(a=in, b=false, sel=reset, out=loadValue);
    Mux16(a=nextValue, b=loadValue, sel=loadOrReset, out=regIn);

    // Passes either the current register output or the
    // incremented version of it as the loopback value.
    Inc16(in=currentOut, out=incOut);
    Mux16(a=currentOut, b=incOut, sel=inc, out=nextValue);

    Register(in=regIn, load=loadReg, out=currentOut, out=out);
}
